#version 400 core

in vec3 position;
in vec2 texCoords;
in vec3 normal;

out vec2 pass_texCoords;
out vec3 surfaceNormal;
out vec3 toLight[4];
out vec3 toCamera;
out float visibility;
out vec4 shadowCoords;

uniform mat4 transformationMatrix;
uniform mat4 projectionMatrix;
uniform mat4 viewMatrix;
uniform vec3 lightPos[4];
uniform vec4 clipPlane;

uniform mat4 toShadowMapSpace;

const float DENSITY = 0.0035;
const float GRADIENT = 5.0;
const float shadowDistance = 100.0;
const float transitionDistance = 10.0;

void main(void) {

    vec4 worldPos = transformationMatrix * vec4(position, 1.0);
    shadowCoords = toShadowMapSpace * worldPos;

    gl_ClipDistance[0] = dot(worldPos, clipPlane);

    vec4 posRelativeToCam = viewMatrix * worldPos;
    gl_Position = projectionMatrix * posRelativeToCam;
    pass_texCoords = texCoords;

    surfaceNormal = (transformationMatrix * vec4(normal, 0.0)).xyz;

    for(int i = 0; i < 4; i++) {
        toLight[i] = lightPos[i] - worldPos.xyz;
    }

    toCamera = (inverse(viewMatrix) * vec4(0.0, 0.0, 0.0, 1.0)).xyz - worldPos.xyz;

    float distance = length(posRelativeToCam.xyz);
    visibility = exp(-pow((distance*DENSITY), GRADIENT));
    visibility = clamp(visibility, 0.0, 1.0);

    distance = distance - (shadowDistance - transitionDistance);
    distance = distance / transitionDistance;
    shadowCoords.w = clamp(1.0-distance, 0.0, 1.0);
}